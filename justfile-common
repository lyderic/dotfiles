common-version := "202409210842"

alias up  := update
alias geo := geolocate
alias var := variables
alias dim := dimensions

# if 'false' updates are unattended
# [up]date distro and config
[group('common')]
[no-exit-message]
update:
	#!/bin/bash
	echo -e "\e[34m[chezmoi]\e[0m"
	chezmoi update
	echo -e "\e[34m[chezmoi (root)]\e[0m"
	sudo chezmoi update
	echo -e "\e[34m[distro:{{distro}}]\e[0m"
	[ -x /sbin/apk ] && {
		sudo apk -U upgrade
		sudo apk -v cache clean
	}
	[ -x /usr/bin/apt ] && {
		[ "{{confirm}}" == "true" ] && yes="-y"
		sudo apt-get ${yes} update
		sudo apt-get ${yes} upgrade
		sudo apt-get ${yes} dist-upgrade
		sudo apt-get ${yes} autoremove
		sudo apt-get ${yes} autoclean
		sudo apt-get ${yes} clean
	}
	[ -x /usr/bin/pacman ] && {
		[ "{{confirm}}" == "true" ] || noconfirm="--noconfirm"
			sudo pacman -Syu ${noconfirm}
			sudo paccache -rk 1
			printf "\e[7;93m"
			reviews=$(sudo pacdiff -o)
			orphans=$(sudo pacman -Qtd)
			[ -z "${reviews}" ] || echo -e "*** REVIEWS ***\n${reviews}"
			[ -z "${orphans}" ] || echo -e "*** ORPHANS ***\n${orphans}"
			printf "\e[m"
	}

# remove archlinux orphans
[group('arch')]
[no-exit-message]
orphans: _check_arch
	pacman -Qtd
	sudo pacman -Qtdq | sudo pacman -Rns -

# deep clean archlinux cache
[group('arch')]
deep-clean: _check_arch
	sudo paccache -ruk0
	sudo pacman -Sc --noconfirm

_check_arch:
	[ -x /usr/bin/pacman ]

# show ANSI codes
[group('common')]
ansi:
	#!/bin/bash
	for i in 1 2 3 4 7 9; do
		echo -ne "\e[${i}m  ${i}\e[m "
	done ; echo
	for i in $(seq 30 37) ; do
		echo -ne "\e[${i}m ${i} "
	done ; echo -e "\e[m"
	for i in $(seq 90 97) ; do
		echo -ne "\e[${i}m ${i} "
	done ; echo -e "\e[m"
	for i in $(seq 40 47) ; do
		echo -ne "\e[${i}m ${i} "
	done ; echo -e "\e[m"
	for i in $(seq 100 107) ; do
		echo -ne "\e[${i}m${i} "
	done ; echo -e "\e[m"

# [geo] where am I?
[group('common')]
geolocate:
	curl -sf "http://ip-api.com/line/?fields=query,city,country,isp"

# [var] evaluate variables
[group('common')]
variables:
	just --evaluate

# [dim] terminal dimensions
[group('common')]
@dimensions:
	echo "$(tput cols) x $(tput lines)"

_help:
	@just --list --no-aliases --unsorted \
		--list-heading=$'\e[34m{{justfile()}}\e[m\n' \
		--list-prefix=' • ' | sed -e 's/ • \[/[/'

# DYNAMIC VARIABLES, SETTINGS AND IMPORTS

distro := `grep ^ID= /etc/os-release | cut -d= -f2`
confirm := "true"
set shell := ["bash","-uc"]
# vim: ft=just
