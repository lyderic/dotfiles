common-version := "202501240912"

alias up  := update

# if 'false' updates are unattended
# update distro and config
[group('common')]
[no-exit-message]
update:
	#!/bin/bash
	echo -e "\e[34m[chezmoi]\e[0m"
	chezmoi update
	echo -e "\e[34m[chezmoi (root)]\e[0m"
	sudo chezmoi update
	echo -e "\e[34m[distro:${distro}]\e[0m"
	[ -x /sbin/apk ] && {
		sudo apk -U upgrade
		sudo apk -v cache clean
	}
	[ -x /usr/bin/apt ] && {
		[ "${confirm}" == "true" ] && yes="-y"
		sudo apt-get ${yes} update
		sudo apt-get ${yes} upgrade
		sudo apt-get ${yes} dist-upgrade
		sudo apt-get ${yes} autoremove
		sudo apt-get ${yes} autoclean
		sudo apt-get ${yes} clean
	}
	[ -x /usr/bin/snap ] && {
		sudo snap refresh
	}
	[ -x /usr/bin/pacman ] && {
		[ "${confirm}" == "true" ] || noconfirm="--noconfirm"
			sudo pacman -Syu ${noconfirm}
			sudo paccache -rk 1
			printf "\e[7;93m"
			reviews=$(sudo pacdiff -o)
			orphans=$(sudo pacman -Qtd)
			[ -z "${reviews}" ] || echo -e "*** REVIEWS ***\n${reviews}"
			[ -z "${orphans}" ] || echo -e "*** ORPHANS ***\n${orphans}"
			printf "\e[m"
	}

{{ if eq .chezmoi.osRelease.id "arch" -}}
# remove archlinux orphans
[group('archlinux')]
[no-exit-message]
orphans: _check_arch
	pacman -Qtd
	sudo pacman -Qtdq | sudo pacman -Rns -

# deep clean archlinux cache
[group('archlinux')]
deep-clean: _check_arch
	sudo paccache -ruk0
	sudo pacman -Sc --noconfirm

_check_arch:
	[ -x /usr/bin/pacman ]
{{- end }}

# show ANSI codes
[group('common')]
ansi:
	#!/bin/bash
	for i in 1 2 3 4 7 9; do
		echo -ne "\e[${i}m  ${i}\e[m "
	done ; echo
	for i in $(seq 30 37) ; do
		echo -ne "\e[${i}m ${i} "
	done ; echo -e "\e[m"
	for i in $(seq 90 97) ; do
		echo -ne "\e[${i}m ${i} "
	done ; echo -e "\e[m"
	for i in $(seq 40 47) ; do
		echo -ne "\e[${i}m ${i} "
	done ; echo -e "\e[m"
	for i in $(seq 100 107) ; do
		echo -ne "\e[${i}m${i} "
	done ; echo -e "\e[m"

# used by vim's ansi plugin
_wait:
	#!/bin/bash
	echo -ne "\e[2;3;34mPlease any key to continue...\e[m"
	read -sn 1
	echo

# where am I?
[group('common')]
geolocate:
	curl -sf "http://ip-api.com/line/?fields=query,city,country,isp"

# terminal dimensions
[group('common')]
@dimensions:
	echo "$(tput cols) x $(tput lines)"

_help:
	@just --list --unsorted --alias-style left --color always \
		--list-heading= --list-prefix='  ' \
		| sed -e "s/\[alias: /\[/"

[private]
v:
	@just --evaluate

# DYNAMIC VARIABLES, SETTINGS AND IMPORTS

export distro := `awk -F= '/^ID=/ {print $2}' /etc/os-release`
export confirm := "true"
set shell := ["bash","-uc"]
# vim: ft=just
